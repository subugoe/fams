buildscript {
    ext {
        kotlin_version = '1.1.4-3'
        vertx_version = '3.4.2'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.0.RC2"
    }
}

plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '1.2.4'
}

apply plugin: 'kotlin'
apply plugin: "io.spring.dependency-management"

group 'de.unigoettingen.sub'
version '0.3.0-SNAPSHOT'

repositories {
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
    jcenter()
}

dependencyManagement {
    imports {
        mavenBom 'com.amazonaws:aws-java-sdk-bom:1.10.77'
    }
}

def watchForChange = 'src/**/*'
def doOnChange = './gradlew classes'

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-runtime:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile "io.vertx:vertx-core:$vertx_version"
    compile "io.vertx:vertx-lang-kotlin:$vertx_version"
    compile "io.vertx:vertx-web:$vertx_version"
    compile "io.vertx:vertx-config:$vertx_version"
    compile 'com.amazonaws:aws-java-sdk-s3'
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile 'com.squareup.okhttp3:mockwebserver:3.7.0'
    testCompile "io.vertx:vertx-unit:$vertx_version"
}

mainClassName = 'io.vertx.core.Launcher'
def mainVerticleName = 'de.unigoettingen.sub.fams.Server'

sourceCompatibility = 1.8
targetCompatibility = 1.8

shadowJar {
    baseName = 'app'
    classifier = 'shadow'

    manifest {
        attributes 'Main-Verticle': mainVerticleName
    }
    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}


run {
  args = [
          'run', mainVerticleName,
          "--redeploy=$watchForChange",
          "--launcher-class=$mainClassName",
          "--on-redeploy=$doOnChange"
  ]
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.2'
}
